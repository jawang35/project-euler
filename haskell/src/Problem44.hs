{- |
Problem 44 - Pentagon Numbers

Pentagonal numbers are generated by the formula, P_n=n(3n−1)/2. The first ten
pentagonal numbers are:

1, 5, 12, 22, 35, 51, 70, 92, 117, 145, ...

It can be seen that P_4 + P_7 = 22 + 70 = 92 = P_8. However, their difference,
70 − 22 = 48, is not pentagonal.

Find the pair of pentagonal numbers, P_j and P_k, for which their sum and
difference are pentagonal and D = |P_k − P_j| is minimised; what is the value
of D?
-}

module Problem44
( answer
, pentagonalNumbers
, isPentagonal
) where

import Data.Fixed (mod')
import qualified Data.Set as Set
import Helpers.Runtime (printAnswerAndElapsedTime)

pentagonalNumbers :: [Int]
pentagonalNumbers = map (\n -> n * (3 * n - 1) `div` 2) [1..]

isPentagonal :: Int -> Bool
isPentagonal number =
    (1 + (sqrt . fromIntegral $ 1 - 4 * 3 * (-2) * number)) `mod'` 6 == 0

answer :: Int
answer = head [ x - y
              | x <- pentagonalNumbers
              , y <- takeWhile (< x) pentagonalNumbers
              , isPentagonal (x + y)
              , isPentagonal (x - y)
              ]

main = printAnswerAndElapsedTime answer
